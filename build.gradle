plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    jcenter()
    google()
}

dependencies {
    implementation group: 'com.google.android', name: 'android', version: '1.5_r3'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

// OSSRH publishing instructions found at https://mrcurious.medium.com/publishing-your-android-kotlin-library-to-maven-central-in-2021-df263a4f2cbc

group = 'io.github.coordinates2country'
version = '1.3'

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.compilerArgs.addAll(['--release', '8'])
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = 'coordinates2country-android'

            from components.java

            pom {
                name = 'Coordinates2Country Android'
                description = 'Fast offline reverse geocoding for Android'
                url = 'https://github.com/coordinates2country/coordinates2country-android'
                licenses {
                    license {
                        name = 'Apache-2.0 License'
                        url = 'https://github.com/coordinates2country/coordinates2country-android/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'Nicolas_Raoul'
                        name = 'Nicolas Raoul'
                        email = 'nicolas.raoul@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/coordinates2country/coordinates2country-android.git'
                    developerConnection = 'scm:git:ssh://github.com/coordinates2country/coordinates2country-android.git'
                    url = 'https://github.com/coordinates2country/coordinates2country-android'
                }
            }
        }
    }

    repositories {
        maven {

            credentials {
                username = "$NEXUS_USERNAME"
                password = "$NEXUS_PASSWORD"
            }

            name = "Coordinates2Country"
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
